html  = _{ SOI ~ value* ~ EOI }
value = _{ text | span | code | banned | other }

text      = { (!("<" ~ "/"? ~ tag_names) ~ ANY)+ }
tag_names = { "span" | "pre" | "strong" }

span       = _{ "<span class=\"" ~ span_inner ~ "</span>" }
span_inner = _{ quote | deadlink | fortune | shiftjis | qst_italic | qst_bold | qst_color }

quote    = { "quote\">" ~ value* }
deadlink = { "deadlink\">" ~ value* }

// If fortunes ever contain tags and not just text, this rule will not match.
// (We don't try to match tags by using `value*`, as it causes problems with matching "</b>" and "<br>")
fortune       = { "fortune\" style=\"color:" ~ fortune_color ~ "\"><br><br><b>" ~ fortune_text ~ "</b>" }
fortune_color = { "#" ~ ASCII_HEX_DIGIT{6} }
fortune_text  = { (!"<" ~ ANY)+ }

shiftjis = { "sjis\">" ~ value* }

qst_italic = { "mu-i\">" ~ value* }
qst_bold   = { "mu-s\">" ~ value* }
qst_color  = { "mu-" ~ (red | green | blue) ~ "\">" ~ value* }
red        = { "r" }
green      = { "g" }
blue       = { "b" }

code   = { "<pre class=\"prettyprint\">" ~ value* ~ "</pre>" }
banned = { "<strong style=\"color: red;\">" ~ value* ~ "</strong>" }

// An unrecognized <span>, <pre>, or <strong> tag (e.g. one with an unrecognized class or style)
other       = { other_start ~ other_inner ~ other_end }
other_start = { "<" ~ !"/" ~ (!">" ~ ANY)+ ~ ">" }
other_inner = { value* }
other_end   = { "</" ~ (!">" ~ ANY)+ ~ ">" }
